plugins {
    id 'org.web3j' version '4.5.11'
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'de.undercouch.download' version '4.0.0'
}

group 'org.web3j'
version '1.0-SNAPSHOT'

[
        'bintray',
        'jacoco',
        'java',
        'javadoc',
        'publish',
        'repositories',
        'spotless'
].each { buildScript ->
    download {
        src "https://raw.githubusercontent.com/web3j/build-tools/master/gradle/$buildScript/build.gradle"
        dest "$rootDir/gradle/$buildScript/build.gradle"
        overwrite true
        quiet true
        onlyIfModified true
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    api "io.github.microutils:kotlin-logging:1.6.10"

    implementation "org.glassfish.jersey.containers:jersey-container-jetty-http:2.29"
    implementation "org.glassfish.jersey.containers:jersey-container-servlet-core:2.29"
    implementation "org.glassfish.jersey.containers:jersey-container-servlet:2.29"
    implementation "org.glassfish.jersey.core:jersey-client:2.29"
    implementation "org.glassfish.jersey.ext:jersey-proxy-client:2.29"
    implementation "org.glassfish.jersey.inject:jersey-hk2:2.29"
    implementation "org.glassfish.jersey.media:jersey-media-json-jackson:2.29"
    implementation "org.glassfish.jersey.inject:jersey-hk2:2.29"
    implementation "org.eclipse.jetty:jetty-server:9.4.27.v20200227"
    implementation "org.eclipse.jetty:jetty-servlet:9.4.27.v20200227"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.61"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.7"
    implementation "io.swagger.core.v3:swagger-annotations:2.1.1"

    implementation "org.web3j:web3j-unit:4.5.11"
    implementation "org.web3j:web3j-evm:4.5.11"
    implementation "org.web3j:core:4.5.11"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.5.2"
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    dependsOn 'generateContractWrappers'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
    dependsOn 'generateTestContractWrappers'
}

sourceSets {
    main.kotlin.srcDirs += "${web3j.generatedFilesBaseDir}/main/java"
    test.kotlin.srcDirs += "${web3j.generatedFilesBaseDir}/test/java"
}
