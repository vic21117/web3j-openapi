description 'web3j-openapi project code generators'

repositories{
    maven { url 'https://repo.gradle.org/gradle/libs-releases' }
}

dependencies {
    implementation("com.pinterest.ktlint:ktlint-core:${versions.ktlint}") {
        exclude group: 'org.apache.commons'
        exclude group: 'org.apache.httpcomponents'
        exclude group: 'org.slf4j'
    }

    implementation "com.pinterest.ktlint:ktlint-ruleset-standard:${versions.ktlint}"
    implementation "com.pinterest.ktlint:ktlint-ruleset-experimental:${versions.ktlint}"
    implementation("org.openapitools:openapi-generator:${versions.openApiGenerator}") {
        exclude group: 'com.github.jknack'
        exclude group: 'javax.activation'
        exclude group: 'javax.validation'
        exclude group: 'javax.xml.bind'
    }

    implementation "org.web3j:codegen:${versions.web3j}"

    implementation "com.squareup:kotlinpoet:${versions.kotlinPoet}"

    testImplementation "org.gradle:gradle-tooling-api:${versions.gradle}"
}


def generatedResources = "$buildDir/generated-resources/main"

sourceSets {
    main {
        // Register an output folder on the main source set:
        output.dir(generatedResources, builtBy: 'generateVersionProperties')
    }
}

task generateVersionProperties {
    description 'Creates a version.properties file with build info that is added to the root of the JAR'
    doLast {
        new File(generatedResources).mkdirs()
        def generated = new File(generatedResources, "openapi-version.properties")
        generated.write("version=$rootProject.version\n")
        generated.append("timestamp=${System.currentTimeMillis()}\n")
    }
}
