apply plugin: 'org.unbroken-dome.test-sets'

testSets {
    integrationTest {
        dirName = 'integration-test'
    }
}

dependencies {
    api project(':web3j-openapi-core')

    implementation "org.web3j:core:${versions.web3j}"

    implementation "org.glassfish.jersey.containers:jersey-container-servlet:${versions.jersey}"
    implementation "org.glassfish.jersey.media:jersey-media-json-jackson:${versions.jersey}"
    implementation "org.glassfish.jersey.inject:jersey-hk2:${versions.jersey}"

    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:${versions.jackson}"

    implementation "org.slf4j:jul-to-slf4j:${versions.slf4j}"

    api "org.eclipse.jetty:jetty-server:${versions.jetty}"
    implementation "org.eclipse.jetty:jetty-servlet:${versions.jetty}"

    implementation "io.swagger.core.v3:swagger-annotations:${versions.swagger}"
    implementation "io.swagger.core.v3:swagger-jaxrs2:${versions.swagger}"

    testImplementation project(':web3j-openapi-client')

    integrationTestImplementation "org.web3j:web3j-unit:${versions.web3j}-SNAPSHOT" // FIXME Release web3j unit
    integrationTestImplementation "org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-jetty:${versions.jersey}"
}

ext {
    generatedProjectName = "testProject"
    outputDirectory = "$projectDir/build/tmp/web3j-openapi/generated"
}

task generateOpenApiProject(type : Exec, group: 'web3j') {
    dependsOn(":web3j-openapi-console:installShadowDist")
    commandLine "$rootDir/console/build/install/web3j-openapi-console-shadow/bin/web3j-openapi",
            'generate',
            '--name', "$generatedProjectName",
            '--context-path', 'api',
            '--package-name', 'com.test',
            '--abi', "$projectDir/src/integration-test/resources/contracts",
            '--bin', "$projectDir/src/integration-test/resources/contracts",
            '--output', "$outputDirectory"
}

compileIntegrationTestKotlin.dependsOn generateOpenApiProject

sourceSets {
    integrationTest.java.srcDirs += [
        "$outputDirectory/$generatedProjectName/server/src/main/java"
    ]
    integrationTest.kotlin.srcDirs += [
        "$outputDirectory/$generatedProjectName/core/src/main/kotlin",
        "$outputDirectory/$generatedProjectName/server/src/main/kotlin",
        "$outputDirectory/$generatedProjectName/server/src/main/java"
    ]
}
